CREATE PROCEDURE CREATE_HISTORY
	@HISTORY_TYPE_ID INT ,
	@DATE DATETIME,
	@HISTORY_ID INT OUTPUT
AS 
BEGIN 
	INSERT INTO HISTORIES (DATE, HISTORY_TYPE_ID)
	VALUES (@DATE, @HISTORY_TYPE_ID)

	SET @HISTORY_ID = SCOPE_IDENTITY();
	
END

GO

CREATE PROCEDURE NEW_HISTORY_EVENT
	@PRODUCT_ITEMS PRODUCT_LIST_TYPE READONLY,	
	@HISTORY_TYPE_ID INT,
	@WAREHOUSE_ID INT,
	@DATE DATETIME,
	@UPDATER INT
AS
BEGIN

	DECLARE @HISTORY_ID INT;
	EXECUTE CREATE_HISTORY @HISTORY_TYPE_ID, @DATE, @HISTORY_ID OUTPUT;

	INSERT INTO HISTORY_PRODUCT (PRODUCT_ID, HISTORY_ID, COUNT, WAREHOUSE_ID)
	SELECT P.PRODUCT_ID, @HISTORY_ID, @UPDATER * P.AMOUNT, @WAREHOUSE_ID
	FROM @PRODUCT_ITEMS P

END;

GO

CREATE PROCEDURE NEW_SALE
	@PRODUCT_ITEMS PRODUCT_LIST_TYPE READONLY,	
	@WAREHOUSE_ID INT,
	@DATE DATETIME
AS
BEGIN
	EXEC NEW_HISTORY_EVENT @PRODUCT_ITEMS, 2, @WAREHOUSE_ID, @DATE, -1
END;
	
GO

CREATE PROCEDURE NEW_SUPPLY
	@PRODUCT_ITEMS PRODUCT_LIST_TYPE READONLY,	
	@WAREHOUSE_ID INT,
	@DATE DATETIME
AS
BEGIN
	EXEC NEW_HISTORY_EVENT @PRODUCT_ITEMS, 1, @WAREHOUSE_ID, @DATE, 1
END;
	
GO

CREATE PROCEDURE CHANGE_PRODUCT_WAREHOUSE_LINK
	@PRODUCT_ID INT,
	@WAREHOUSE_ID INT,
	@COUNT INT
AS
BEGIN

	IF EXISTS(SELECT * FROM WAREHOUSE_PRODUCT WHERE PRODUCT_ID = @PRODUCT_ID AND WAREHOUSE_ID = @WAREHOUSE_ID)
		UPDATE WAREHOUSE_PRODUCT
		SET COUNT = COUNT + @COUNT
		WHERE PRODUCT_ID = @PRODUCT_ID AND WAREHOUSE_ID = @WAREHOUSE_ID
	ELSE
		INSERT INTO WAREHOUSE_PRODUCT (WAREHOUSE_ID, PRODUCT_ID, COUNT)
		VALUES (@WAREHOUSE_ID, @PRODUCT_ID, @COUNT)

END;

GO
